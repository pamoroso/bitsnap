(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 2-Mar-2025 14:22:15" {DSK}<home>paolo>il>bitsnap>BITSNAP.;8 6857   

      :EDIT-BY "PA"

      :CHANGES-TO (FNS PROMPTFORW)

      :PREVIOUS-DATE " 2-Mar-2025 13:16:14" {DSK}<home>paolo>il>bitsnap>BITSNAP.;7)


(PRETTYCOMPRINT BITSNAPCOMS)

(RPAQQ BITSNAPCOMS
       ((* A screenshot capture tool for Medley Interlisp. It can capture screenshots of the the full
           screen, a window or its interior, or an arbitrary area.)
        (FNS BMTOPBM PROMPTFORW SNAP)
        [DECLARE%: DONTEVAL@LOAD DOCOPY
               (ADDVARS (BackgroundMenuCommands
                         ("Screenshot" '(SNAP NIL T)
                                "Captures and saves the full screen."
                                (SUBITEMS ("Screen" '(SNAP NIL T)
                                                 "Captures and saves the full screen.")
                                       ("Window" '(SNAP 'WINDOW T)
                                              
                                             "Captures and saves a window with title bar and border."
                                              (SUBITEMS ("Full" '(SNAP 'WINDOW T)
                                                               
                                             "Captures and saves a window with title bar and border."
                                                               )
                                                     ("Interior" '(SNAP 'INTERIOR T)
                                                            
                                          "Captures and saves a window without title bar and border."
                                                            )))
                                       ("Selected" '(SNAP 'SELECTION T)
                                              "Captures and saves an arbitrary screen area."]
        (P (SETQ BackgroundMenu))
        (FILES BITMAPFNS)))



(* A screenshot capture tool for Medley Interlisp. It can capture screenshots of the the full screen, 
a window or its interior, or an arbitrary area.)

(DEFINEQ

(BMTOPBM
  [LAMBDA (BITMAP FILE)                                      (* ; "Edited 28-Feb-2025 18:12 by PA")
                                                             (* Save BITMAP to FILE in PBM format 
                                                             and return BITMAP.)
    (LET [(WIDTH (BITMAPWIDTH BITMAP))
          (HEIGHT (BITMAPHEIGHT BITMAP))
          (STREAM (OPENSTREAM FILE 'OUTPUT]
         (RESETLST
             (RESETSAVE NIL (LIST 'CLOSEF STREAM))
             (PRINTOUT STREAM "P1" T)
             (PRINTOUT STREAM WIDTH %, HEIGHT T T)           (* Lines of PBM files should have no 
                                                             more than 70 characters.)
             (for Y from (SUB1 HEIGHT) to 0 by -1
                do (bind (N _ 0) for X from 0 to (SUB1 WIDTH)
                      do (if (EQP N 70)
                             then (TERPRI STREAM)
                                  (SETQ N 0))
                         (PRINTOUT STREAM (BITMAPBIT BITMAP X Y))
                         (SETQ N (ADD1 N)))
                   (TERPRI STREAM)))
         BITMAP])

(PROMPTFORW
  [LAMBDA NIL                                                (* ; "Edited  2-Mar-2025 14:20 by PA")
                                                             (* ; "Edited 28-Feb-2025 18:36 by PA")

         (* Prompt the user to indicate a window. Displays in the prompt window a message 
         that advises to place the pointer over a window, waits for the user to click a 
         mouse button, and returns the window the pointer is over.)

    (FRESHLINE PROMPTWINDOW)
    (MOUSECONFIRM "Place the pointer over the desired window." "Click any button to proceed." 
           PROMPTWINDOW T)
    (WHICHW])

(SNAP
  [LAMBDA (WHAT WHERE)                                       (* ; "Edited  2-Mar-2025 13:14 by PA")

         (* Capture the area of the screen indicated by WHAT, save it to the destination 
         indicated by WHERE, and return a bitmap of the area.
         WHAT can be NIL for the full screen, WINDOW for a window including the title bar 
         and border, INTERIOR for a window with no title or border, or SELECTION for an 
         arbitrary area. WHERE can be NIL to not save, T to prompt the user for a file 
         name, or a file name.)

    (LET [(BITMAP (SELECTQ WHAT
                      (WINDOW (WINDOW.BITMAP (PROMPTFORW)))
                      (INTERIOR (BITMAPCOPY (PROMPTFORW)))
                      (SELECTION (SNAPW))
                      (BITMAPCOPY (SCREENBITMAP]
         [AND WHERE (BMTOPBM BITMAP (if (EQ WHERE T)
                                        then (FRESHLINE PROMPTWINDOW)
                                             (OR (PROMPTFORWORD "File name:" "screenshot.pbm" NIL 
                                                        PROMPTWINDOW)
                                                 "screenshot.pbm")
                                      else (MKSTRING WHERE]  (* SNAPW already displays the captured 
                                                             area, so no need to do it again.)
         (if (NOT (EQ WHAT 'SELECTION))
             then (WINDOWBM (if (NULL WHAT)
                                then (SHRINKBITMAP BITMAP 2 2)
                              else BITMAP)))
         BITMAP])
)
(DECLARE%: DONTEVAL@LOAD DOCOPY 

(ADDTOVAR BackgroundMenuCommands
          ("Screenshot" '(SNAP NIL T)
                 "Captures and saves the full screen."
                 (SUBITEMS ("Screen" '(SNAP NIL T)
                                  "Captures and saves the full screen.")
                        ("Window" '(SNAP 'WINDOW T)
                               "Captures and saves a window with title bar and border."
                               (SUBITEMS ("Full" '(SNAP 'WINDOW T)
                                                
                                             "Captures and saves a window with title bar and border."
                                                )
                                      ("Interior" '(SNAP 'INTERIOR T)
                                             
                                          "Captures and saves a window without title bar and border."
                                             )))
                        ("Selected" '(SNAP 'SELECTION T)
                               "Captures and saves an arbitrary screen area."))))
)

(SETQ BackgroundMenu)

(FILESLOAD BITMAPFNS)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (2161 5684 (BMTOPBM 2171 . 3366) (PROMPTFORW 3368 . 4026) (SNAP 4028 . 5682)))))
STOP
